/*
 * File: optmodel.tppl
 * Description: Probabilistic model taken from optimal metabarcoding
 * Compilation:
 *  tpplc models/optimal_metabarcoding/optmodel.tppl -m 'smc-apf' --resample align --output optmodel
 * Execution with 500000 particles and 100 sweeps (homogenate data H.json or lysate data L.json): 
 * ./optmodel models/data/optmetabar/H.json 500000 100 > output/H.json
 */

/*
 * Model function
 * Data:
 *   Homogenate data in H.json or lysate data in L.json: 
 * Priors: 
 *   From optimal metabarcoding paper, Iwaszkiewicz-Eggebrecht et al., 2023
 *
 */
model function myModel(dataset: Int[][]): ()  {
  assume k ~ Gamma(1.0, 10.0);
  assume logTheta ~ Gaussian(0.0, 2.0); 
  let theta = exp(logTheta);
  
  assume tau ~ Gamma(3.0, (1.0/27.0));
  let sigma = sqrt(1.0 / tau); //sigma2 sampled from InverseGamma
  assume mu ~ Gaussian(6.5, sigma); 

  let nsamples = length(dataset); // nsamples is total number of samples, with index j
  let logC = iid(gaussian, GaussianParam{mean = mu, dev = sigma}, nsamples); // sample j logC's
  let c = sapply(logC, exp); 

  for j in 1 to nsamples { 
    for i in 1 to 4 { //i is index of spike-in species, in this example 1 to 4
      if neqi(dataset[j][i], 0) {
        observe Real(dataset[j][i]) ~ Gamma(k, c[j] * theta);
      }
    }
  }

  //return () //Not returning samples in this version
}