/*
 * File: optmodel_1c.tppl
 * Description: Probabilistic model taken from optimal metabarcoding, draft 2023-09-13
 * Compilation:
 *  tpplc models/optmetabar/optmodel_1c.tppl -m 'smc-bpf' --resample align --output optmodel_1c
 * Execution with 100000 particles and 100 sweeps (homogenate data H.json or lysate data L.json): 
 * ./optmodel_1c models/data/H_real.json 100000 100 > output/H_1c.json
 */

/*
 * Model function
 * Data:
 *   Homogenate data in H.json or lysate data in L.json: 
 * Priors: 
 *   From optimal metabarcoding paper, Iwaszkiewicz-Eggebrecht et al., 2023
 * Posterior:
 *   returning k at the moment
 */
model function myModel(dataset: Real[][]) : Real  {
  assume k ~ Gamma(1.0, 10.0);
  assume logTheta ~ Gaussian(0.0, 2.0); 
  let theta = exp(logTheta);
  
  assume tau ~ Gamma(3.0, (1.0/27.0));
  let sigma = sqrt(1.0 / tau); //sigma2 sampled from InverseGamma
  assume mu ~ Gaussian(6.5, sigma); 
  assume logC ~ Gaussian(mu, sigma); 
  let c = exp(logC);

  let nsamples = length(dataset); // nsamples is total number of samples, with index j
  for j in 1 to nsamples { 
    for i in 1 to 4 { //i is index of spike-in species, in this example 1 to 4
      if dataset[j][i] > 0.0 {
        observe dataset[j][i] ~ Gamma(k, c*theta);
      }
    }
  }
  return(k); 
}